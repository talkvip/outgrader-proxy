apply plugin: 'application'

dependencies {
	compile project(':outgrader-proxy-core')
	compile project(':outgrader-proxy-properties')
	compile project(':outgrader-proxy-external')
	compile project(':outgrader-proxy-statistics')
	compile project(':outgrader-proxy-advertisment-processor')
	compile project(':outgrader-proxy-advertisment-storage')
}

mainClassName = 'com.outgrader.proxy.Outgrader'

startScripts {
	classpath += files('src/dist/config')
	
	doLast {
		def windowsScriptFile = file getWindowsScript()
		def unixScriptFile    = file getUnixScript()
		
		//fixing classpath problem
		windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\config', '%APP_HOME%\\config')
		unixScriptFile.text    = unixScriptFile.text.replace('$APP_HOME/lib/config', '$APP_HOME/config')
	}
}

installApp {
	exclude '**/*outgrader.properties'
	exclude '**/logback.xml'
}

distZip {
	exclude '**/*outgrader.properties'
	exclude '**/logback.xml'
}

distTar {
	exclude '**/*outgrader.properties'
	exclude '**/logback.xml'
}